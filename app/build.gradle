apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'realm-android'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        // Original URL no longer available, so hosting the hard coded JSON at emaxwell.info.
        //buildConfigField "String", "ANF_BASE_URL", '"https://www.abercrombie.com/anf/nativeapp/Feeds"'
        buildConfigField "String", "ANF_BASE_URL", '"http://emaxwell.info/s/"'
        applicationId "software.credible.abercrombiefitchkata"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            // Original URL no longer available, so hosting the hard coded JSON at emaxwell.info.
            //buildConfigField "String", "ANF_BASE_URL", '"https://www.abercrombie.com/anf/nativeapp/Feeds"'
            buildConfigField "String", "ANF_BASE_URL", '"http://emaxwell.info/s/"'
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dexOptions {
        jumboMode true
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            // Configure max heap size of the test JVM
            maxHeapSize = '2048m'

            // Specify max number of processes (default is 1)
            maxParallelForks = 3

            // Specify max number of test classes to execute in a test process
            // before restarting the process (default is unlimited)
            forkEvery = 250

            testLogging {
                events "passed", "skipped", "failed", "standardError" //, "standardOut"
            }
        }
        execution 'ANDROID_TEST_ORCHESTRATOR'
    }

    lintOptions {
        disable 'InvalidPackage',
                'InconsistentLayout', // layout-w900dp/promotions_list.xml trips this but is expected.
                'GoogleAppIndexingWarning',
                'UnusedResources' // Kotlin doesn't catch used layouts from kotlin files as far as I can tell.
                                  // https://youtrack.jetbrains.com/issue/KT-7729.
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Android Standard Libs
    implementation 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.android.support:support-v4:27.0.2'
    implementation 'com.android.support:recyclerview-v7:27.0.2'
    implementation 'com.android.support:design:27.0.2'
    implementation ('com.android.support.test.espresso:espresso-contrib:3.0.1') {
        exclude group: 'junit', module: 'junit'
    }
    implementation 'android.arch.lifecycle:extensions:1.1.0'
    kapt 'android.arch.lifecycle:compiler:1.1.0'


    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Retrofit, OkHttp, Gson JSON Adapter
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.okhttp3:okhttp:3.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'

    // RxJava2
    implementation 'io.reactivex.rxjava2:rxjava:2.1.9'

    // Realm Recycler View Adapter Support
    implementation 'io.realm:android-adapters:2.1.0'

    // Picasso for Async Image Loading
    implementation 'com.squareup.picasso:picasso:2.5.2'

    // Testing
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support:support-annotations:27.0.2'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test:rules:1.0.1'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestUtil 'com.android.support.test:orchestrator:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-intents:3.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-web:3.0.1'

    // MockWebServer to simulate HTTP calls in unit tests
    androidTestImplementation 'com.squareup.okhttp:mockwebserver:2.4.0'


}
repositories {
    mavenCentral()
}

